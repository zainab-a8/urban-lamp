apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:grgit:1.9.3'
    }
}

ext {
    android_support_version = '25.2.0'
}

if(rootProject.file("keystore.properties").exists()) {
    // From https://developer.android.com/studio/publish/app-signing.html
    // to load the signing config file
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    android {
        signingConfigs {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
   }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.jmstudios.redmoon"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 32
        versionName "3.1.2"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ eventBusIndex : 'com.jmstudios.redmoon.event.EventBusIndex' ]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.debug'
            def repo = org.ajoberstar.grgit.Grgit.open()
            def commitId = "${repo.log().get(0).getAbbreviatedId()}"
            if (repo.status().isClean()) {
                versionNameSuffix "-$commitId"
            } else {
                versionNameSuffix "-$commitId (dirty)"
            }
        }
    }
    productFlavors {
        fdroid {
        }
        playstore {
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    lintOptions {
        disable 'NewApi'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$android_support_version"
    compile "com.android.support:design:$android_support_version"
    compile 'com.luckycatlabs:SunriseSunsetCalculator:1.2'
    compile 'com.github.paolorotolo:appintro:4.1.0'
    compile 'de.cketti.library.changelog:ckchangelog:1.2.2'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'me.smichel.android:kpreferences:0.7.0'
    playstoreCompile 'com.github.hotchemi:android-rate:1.0.1'
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.0.1'
}

repositories {
    mavenCentral()
}
