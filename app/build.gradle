apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:grgit:1.9.3'
    }
}

ext {
    android_support_version = '26.1.0'
}

if(rootProject.file("keystore.properties").exists()) {
    // From https://developer.android.com/studio/publish/app-signing.html
    // to load the signing config file
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    android {
        signingConfigs {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
   }
}

android {
    compileSdkVersion 26
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.jmstudios.redmoon"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 36
        versionName "3.3.2"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.debug'
            def repo = org.ajoberstar.grgit.Grgit.open()
            def commitId = "${repo.log().get(0).getAbbreviatedId()}"
            if (repo.status().isClean()) {
                versionNameSuffix "-$commitId"
            } else {
                versionNameSuffix "-$commitId (dirty)"
            }
        }
    }
    flavorDimensions "dist"
    productFlavors {
        fdroid {
            dimension "dist"
        }
        playstore {
            dimension "dist"
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    lintOptions {
        disable 'NewApi'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:support-compat:$android_support_version"
    implementation "com.android.support:appcompat-v7:$android_support_version"
    implementation "com.android.support:design:$android_support_version"
    implementation 'com.luckycatlabs:SunriseSunsetCalculator:1.2'
    implementation 'com.github.paolorotolo:appintro:4.1.0'
    implementation 'de.cketti.library.changelog:ckchangelog:1.2.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'me.smichel.android:kpreferences:0.7.0'
    playstoreImplementation 'com.github.hotchemi:android-rate:1.0.1'
    // annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.0.1'
}

repositories {
    mavenCentral()
}
